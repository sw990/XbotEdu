plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2021.2.2"
    id 'org.hidetake.ssh' version "2.9.0"
    id 'checkstyle'
    id 'jacoco'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def ROBOT_MAIN_CLASS = "competition.Main"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        frcJavaArtifact('frcJava') {
            targets << "roborio"
            // Debug can be overridden by command line, for use with VSCode
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact('frcStaticFileDeploy') {
            // The directory below is the local directory to deploy
            files = fileTree(dir: 'src/main/deploy')
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

// Maven central needed for JUnit
repositories {
    mavenCentral()
    jcenter()
    
    maven {
        url "https://mvnrepository.com/artifact/com.google.inject/guice"
        url "http://first.wpi.edu/FRC/roborio/maven/release"
        url "http://devsite.ctr-electronics.com/maven/release/"
        url "http://www.revrobotics.com/content/sw/max/sdk/maven/"
    }
    
    flatDir {
       dirs 'SeriouslyCommonLib/build/libs'
   }
}

// Set this to true to enable desktop support.
def includeDesktopSupport = true

List<String> wpilibDesktopJni(String platform) {
    return ["edu.wpi.first.thirdparty.frc2021.opencv:opencv-jni:${wpi.opencvVersion}:${platform}@jar".toString(),
            "edu.wpi.first.hal:hal-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.wpiutil:wpiutil-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.ntcore:ntcore-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.cscore:cscore-jni:${wpi.wpilibVersion}:${platform}@jar".toString()]
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 4.
dependencies {
    implementation "xbot.common:scl"
    implementation wpi.deps.wpilib()
    nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)


    implementation wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

    testImplementation 'junit:junit:4.12'
    testImplementation(wpilibDesktopJni(wpi.platforms.desktop))
    testImplementation(wpi.deps.vendor.jni(wpi.platforms.desktop))


    compile group: 'com.google.inject', name: 'guice', version: '4.2.2'
    compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.2.2'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'org.json', name:'json', version: '20201115'

    // Enable simulation gui support. Must check the box in vscode to enable support
    // upon debugging
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, false)
    simulation "edu.wpi.first.halsim:halsim_ds_socket:${wpi.wpilibVersion}:${wpi.platforms.desktop}@zip"
}

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
}


remotes {
    rio {
        host = 'roboRIO-488-frc.local'
        user = 'admin'
        knownHosts = allowAnyHosts
    }
}

task('copyResources') {
    doLast {
        ssh.run {
            session(remotes.rio) {
                // execute 'mkdir ~/449_resources', ignoreError: true
                // execute 'mkdir /488Logs', ignoreError: true
                put from: fileTree('SeriouslyCommonLib/lib/log4jConfig/log4j.xml'), into: '/home/lvuser/'
                //execute ". /etc/profile.d/natinst-path.sh; /usr/local/frc/bin/frcKillRobot.sh -t -r", ignoreError: true
            }
        }
    }
}

checkstyle {
    toolVersion = '8.7'
    configFile = file('SeriouslyCommonLib/xbotcheckstyle.xml')
}

checkstyleMain{
    logging.setLevel(LogLevel.WARN)
}

checkstyleTest{
    logging.setLevel(LogLevel.WARN)
}

task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    configProperties.basedir = file('SeriouslyCommonLib')
    
    // Empty classpath
    classpath = files()
}

test {
    reports {
        junitXml.enabled = true
    }               
}

tasks.withType(Test) {
    testLogging.showStandardStreams = true
    
    afterTest { desc, result -> 
        logger.quiet "Executed test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }

    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        logger.quiet "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

// always generate the coverage report after the tests run
test.finalizedBy { jacocoTestReport }