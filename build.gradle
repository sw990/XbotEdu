import edu.wpi.first.gradlerio.deploy.roborio.RoboRIO

plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2022.3.1" 
    id "eclipse"
    id "com.diffplug.eclipse.apt" version "3.39.0"
    id 'checkstyle'
    id 'jacoco'
    id 'org.hidetake.ssh' version "2.9.0"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

def ROBOT_MAIN_CLASS = "competition.Main"

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                    // jvmArgs <<
                    //     " -Dcom.sun.management.jmxremote=true" +
                    //     " -Dcom.sun.management.jmxremote.port=1099" +
                    //     " -Dcom.sun.management.jmxremote.local.only=false" +
                    //     " -Dcom.sun.management.jmxremote.ssl=false" +
                    //     " -Dcom.sun.management.jmxremote.authenticate=false" +
                    //     " -Djava.rmi.server.hostname=10.4.88.2"
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for JNI.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = false

// Maven central needed for JUnit
repositories {
    mavenCentral()
    jcenter()
    
    maven {
        url "https://mvnrepository.com/artifact/com.google.dagger/dagger"
        url "https://mvnrepository.com/artifact/com.google.dagger/dagger-compiler"
        url "https://first.wpi.edu/FRC/roborio/maven/release"
        url "https://devsite.ctr-electronics.com/maven/release/"
    }
    
    flatDir {
       dirs 'SeriouslyCommonLib/build/libs'
   }
}

List<String> wpilibDesktopJni(String platform) {
    return ["edu.wpi.first.thirdparty.frc2021.opencv:opencv-jni:${wpi.opencvVersion}:${platform}@jar".toString(),
            "edu.wpi.first.hal:hal-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.wpiutil:wpiutil-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.ntcore:ntcore-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.cscore:cscore-jni:${wpi.wpilibVersion}:${platform}@jar".toString()]
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 4.
dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation 'junit:junit:4.12'

    implementation "xbot.common:scl"
    implementation group: 'log4j', name: 'log4j', version: '1.2.17'
    implementation group: 'org.json', name:'json', version: '20201115'

    implementation 'com.google.dagger:dagger:2.43.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.43.2'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.43.2'
}

wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

// Our custom gradle stuff after this line

remotes {
    rio {
        host = 'roboRIO-488-frc.local'
        user = 'admin'
        knownHosts = allowAnyHosts
    }
}

task('copyResources') {
    doLast {
        ssh.run {
            session(remotes.rio) {
                // execute 'mkdir ~/449_resources', ignoreError: true
                // execute 'mkdir /488Logs', ignoreError: true
                put from: fileTree('SeriouslyCommonLib/lib/log4jConfig/log4j.xml'), into: '/home/lvuser/'
                //execute ". /etc/profile.d/natinst-path.sh; /usr/local/frc/bin/frcKillRobot.sh -t -r", ignoreError: true
            }
        }
    }
}

tasks.getByName('eclipseJdtApt') {
    dependsOn gradle.includedBuilds*.task(':eclipseJdtApt')
}
tasks.getByName('eclipseJdt') {
    dependsOn gradle.includedBuilds*.task(':eclipseJdt')
}
tasks.getByName('eclipseFactorypath') {
    dependsOn gradle.includedBuilds*.task(':eclipseFactorypath')
}
eclipse {
	// re-generate APT configuration on Gradle project synchronisation
	synchronizationTasks eclipseJdtApt, eclipseJdt, eclipseFactorypath

	jdt.apt {
        genSrcDir = file('bin/generated/sources/annotationProcessor/java/main')
        genTestSrcDir = file('bin/generated/sources/annotationProcessor/java/test')
    }

    classpath.file {
        beforeMerged { classpath -> 
            sourceSets.main.java.srcDirs += "bin/generated/sources/annotationProcessor/java/main"
            sourceSets.test.java.srcDirs += "bin/generated/sources/annotationProcessor/java/test"
        }

        whenMerged { classpath ->
            classpath.entries.each { entry -> 
                if (entry.path.contains('bin/generated')) {
                    entry.entryAttributes['ignore_optional_problems'] = true
                }
            }
        }
    }
}

checkstyle {
    toolVersion = '8.7'
    configFile = file('SeriouslyCommonLib/xbotcheckstyle.xml')
}

task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    configProperties.basedir = file('SeriouslyCommonLib')
    
    // Empty classpath
    classpath = files()
}

test {
    reports {
        junitXml.required = true
    }               
}

// force test project in SCL to compile so Dagger generation runs
tasks.getByName('compileTestJava') {
    dependsOn gradle.includedBuilds*.task(':compileTestJava')
}
tasks.getByName('clean') {
    dependsOn gradle.includedBuilds*.task(':clean')
}

tasks.withType(Test) {
    testLogging.showStandardStreams = true
    
    afterTest { desc, result -> 
        logger.quiet "Executed test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }

    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        logger.quiet "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

// always generate the coverage report after the tests run
test.finalizedBy { jacocoTestReport }